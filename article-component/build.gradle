if (isBuildModuleArticle.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    /**
     * 资源同名问题处理
     * 每个子模块给资源名添加前缀，具体做法是在每个子模块的build.gradle文件的android块内添加一行代码：resourcePrefix “资源名前缀”：
     */
    resourcePrefix "article_component_"
    compileSdkVersion Android.compileSdkVersion
    buildToolsVersion Android.buildToolsVersion

    defaultConfig {
        if (isBuildModuleArticle.toBoolean()) {
            applicationId "com.kotlin.android.article.component"
        }
        minSdkVersion Android.minSdkVersion
        targetSdkVersion Android.targetSdkVersion
        versionCode Android.versionCode
        versionName Android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true  //压缩优化

            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        dataBinding = true
        // for view binding :
        // viewBinding = true
    }

    sourceSets {
        main {
            if (isBuildModuleArticle.toBoolean()) {
                manifest.srcFile 'src/main/java/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //release模式下排查debug文件夹中的所有文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    repositories {
        flatDir {
            dirs  'libs'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation Support.kotlin_stdlib_jdk8
    testImplementation Support.junit
    androidTestImplementation Support.junit_ext
    androidTestImplementation Support.espresso_core

    // add lib
    implementation project(path: ':core')
    implementation project(path: ':app-api')
    implementation project(path: ':image')
    implementation project(path: ':mtime-ktx')
    implementation project(path: ':widget')
    implementation project(path: ':ugc-detail-component')
    implementation project(path: ':comment-component')
    implementation project(path: ':app-router')
    implementation project(path: ':user')
    implementation project(path: ':player')
    implementation project(path: ':js-sdk')
    implementation project(path: ':share')
    implementation project(path: ':image-component')
    //组件中依赖阿里路由编译框架
    kapt Dependencies.arouter_compiler
}